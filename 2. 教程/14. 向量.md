![avatar](../_images/HelpTutorial.png)

# 教程14：向量

集中精神，因为我们即将讲解一些非常难懂的东西。如果你能把这个东西弄明白，那么你的水平已经上升了一大截。接触一种新的技术的时候就需要通过练习，编写自己的程序以掌握它。强烈建议在尝试这个教程之前，先去学习有关屏幕坐标的章节。如果你已经学过了，那么就让我们继续吧。

说了这么多，是时候进入我们今天的主题了。这篇教程讲解向量，它们为什么会这么有用以及该如何使用它们。

在这篇教程中，我们指挥覆盖最基础的部分，对于比较高级的部分，可以查看我们下一个向量项目。

## 什么是向量

什么是向量，简单的说：

一个向量是将多个数字看作是一个整体。

让我们进入更多细节。

在生活中的一些东西可以用一个单独的值来度量。以温度举例，它只用一个温度值来表示，除此之外就没有其它数值了。

我们成这些值为**标量**。高度，重量，音量、容量等都是**标量**的例子。

但是，如果我们想描述一个事物，比如**位置**，该怎么办呢？我们生活在一个三维的世界，用单独一个数字来表示位置并不能给我们有用的信息。

让我们用在屏幕中间的圆圈来举例，下面的代码将在屏幕中间画一个圆：

```
  1. loop
  2.     clear()
  3.
  4.     circle( gWidth() / 2, gHeight() / 2, 100, 16, white, false )
  5.
  6.     update()
  7. repeat
```

非常简单，我们用一个单独的循环，用来在屏幕中间画一个圆。

我们把圆画在```gwidth() / 2, gheight() / 2```的位置，这个位置刚好就是屏幕的正中心。

现在如果一个人问：“那个圆在哪里？”，如果我们回答：“它在屏幕宽度的二分之一处。“，你发现问题了吗？

屏幕的中间位置，在**x轴**是```gwidth() / 2```，在**y轴**是```gheight() / 2```。也就是说这个位置是两条中线的交点处。

* 竖直方向（与y轴平行），在x轴```gidth() / 2```的位置与x轴相交
* 水平方向（与x轴平行），在y轴```gheight() / 2```的位置与y轴相交

要定位一个点的时候，这两个值是最基本的。如果只有一个值，那么我们会得到一条直线，上面的点都是满足条件的。所以只有一个值，不足以在屏幕上定位一个点。

你知道吗？这种表示坐标的方法叫做“笛卡尔”，这种方法是René Descartes于17世纪发明的，用以建立欧几里得几何（平面几何）与代数的连接。

一个**向量**在这里就可以同时提供表示位置的两个信息。

## 在程序中使用向量

设置向量非常地容易，在下面的程序中我们使用一个向量来把圆圈画在屏幕中间。注意向量的用法：

```
  1. position = { gWidth() / 2, gHeight() / 2 }
  2. 
  3. loop
  4.     clear()
  5.
  6.     circle( position.x, position.y, 100, 16, white, false )
  7.
  8.     update()
  9. repeat
```

正如你所见，第一行我们定义了一个叫```position```的变量，这个变量在一对花括号```{}```保存了一组坐标。

注意这里的花括号，如果你在FUZE的其他地方看到了它，那么它就表示一个**向量**！

在FUZE中，我们可以非常容易地定义一个向量变量，把我们想要的数据放到花括号中，而这里比较聪明的一点是，FUZE在后台会创建一个有**x**和**y**两个值的结构体。在我们的例子中，我们现在就有```position.x```与```position.y```两个属性。

## 使用向量来移动圆圈

还记得在有关于屏幕的教程中，我们学到了如何通过改变圆圈的**x**和**y**坐标来移动圆圈的例子吗？在这个教程中，我们会升级那个程序。

因为向量是把多个值看成是一个整体，我们可以对整个向量进行操作：

```
  1. position = { gWidth() / 2, gHeight() / 2 }
  2. 
  3. loop
  4.     clear()
  5.    
  6.     joy = controls( 0 )
  7.     position += { joy.lx, -joy.ly } * 8
  8.
  9.     circle( position.x, position.y, 100, 16, white, false )
 10.
 11.     update()
 12. repeat
```

看这段简洁的代码！

我们不在使用两个分开的变量**x**和**y**，取而代之的是，我们可以将整个向量赋值给**Joy-Con**的左摇杆。注意我们把```joy.lx```和```joy.ly```一起添加到一个向量中。

我们用8乘以整个向量来增大移动速度，不然的话，移动速度会非常慢。

记住，因为**y**的0是在屏幕顶端的，而```gheight()```得到的值表示的位置是屏幕底端，我们必须添加一个减号（-）在```joy.ly```之前来使我们向上按动摇杆的时候，圆圈会往上移动。

我们再往下分解，第7行我们把```joy.lx```加到```position.x```，同时把```joy.ly```加到```position.y```。

这里本来两行代码变成一行了。

让我们把整个移动过程变得更高级一点，因为我们整个教程就是一个拔高的过程。

现在，我们放开摇杆的时候，圆圈的移动会停下来，这可能是我们再某些时候想要的效果，不过添加一些减速效果会更加真实，也会更令人满意。

我们可以通过添加几行代码就可以实现整个效果，并且向量会让代码变得非常简洁。

为了实现一个减速效果，我们需要一个速度变量，修改代码如下所示：

```
  1. position = { gWidth() / 2, gHeight() / 2 }
  2. velocity = { 0, 0 }
  3. slowdown = 0.9
  4. loop
  5.     clear()
  6.    
  7.     joy = controls( 0 )
  8.     velocity += { joy.lx, -joy.ly } * 8
  9.     position += velocity
 10.     velocity *= slowdown
 11.    
 12.     circle( position.x, position.y, 100, 16, white, false )
 13.    
 14.     update()
 15. repeat  
```

现在我们在程序的开头有一个新的变量```velocity```，整个变量保存了一个0向量```{0,0}```

在第8行，我们没有直接增大```position```变量，而是增大```velocity```向量。通过把两者分开，我们能够控制在操作摇杆时减速度是多少。

在第10行，我们对```velocity```做了一个乘法。在每次循环的时候我们都用```slowdown```和```velocity```相乘。这样我们就持续的缩小向量中的每一个值。这只会在我们放开摇杆的时候才有效果。因为我们在推动摇杆时，我们是在持续的读取数值并且设置给```velocity```。

当我们放开摇杆的时候，第10行的效果就会显现出来了，施加到圆圈的速度就会不断减小。这就会呈现出一个减速效果。

为了使程序更加合理，```slowdown```变量的值是从0到1。却接近0，减速效果越明显。到0的时候，移动会立即停止。当值接近1，减速变得越慢且越不明显。到1的时候，圆圈不会减速。

## 用向量表示颜色

你或许不知道，在屏幕上的所有颜色都是由红色，绿色和蓝色的光混合而成。

当我们设置一个向量，FUZE会给我们一个结构体，包含```.x```和```.y```两个属性。其实与此同时，FUZE也会自动地创建```.z```和```.w```，一共会给我们4个属性。如果我们不在向量中定义这些值，那么他们默认值为0.

FUZE中向量很酷的一点是，我们也可以同过```.r```,```.g```,```.b```,```.a```来访问整个是个值，他们分别代表颜色中的红，绿，蓝还有透明度。

也就是说，我们可以用向量表示颜色。我们可以指定每种颜色的数量大小还有透明度的大小。

这些数字的值都在0和1之间。当数值为0的时候表示没有颜色，当值为1的时候颜色值最大。然我们以红色```red```来举例。

在FUZE中，```red```其实是一个向量的```{1, 0, 0, 1}```的一个标签。只有红色，没有绿色，也没有蓝色且不透明。

比如说我们想创建一个颜色：

```
  1. col = { 0.5, 0.8, 0.1, 1 }
  2.
  3. loop
  4.     clear( col )
  5.     update()
  6. repeat
```

这里，我们在第一行设置了一个向量。在循环里面，我们用创建的颜色来覆盖整个屏幕。能看到满屏的薄荷绿。

如果我们想增加蓝色，我们可以这样做：

```
  1. col = { 0.5, 0.8, 0.1, 1 }
  2.
  3. loop
  4.     clear( col )
  5.     col.b += 0.001
  6.     update()
  7. repeat
```

执行程序，我们能看到薄荷绿慢慢地变成了蓝色，非常棒。

## 复习

一个向量就是把多个数字看成是一个整体。

当你看到一对花括号，那么就代表这个变量是一个向量。

我们可以使用他们做很多事情，最主要的，我们可以用他们来表示坐标和颜色。

这边教程只涉及到向量最基本的知识。向量可以用在很多非常有用的数学计算中，我们会在之后来讲解这部分内容。

很棒，我们已经学到了高级的知识。尝试在你自己的项目中使用向量，使用它们来控制对象的位置，或者改变对象的颜色等等。掌握了向量的使用，相当于打开了一个新世界的大门。

我们在下一篇教程中见！

#### 用到的函数和关键字

```circle()```,```clear()```,```loop```,```repeat```,```update()```
